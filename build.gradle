plugins {
    id 'nebula.release' version '15.1.0'
    id 'com.jfrog.bintray' version '1.8.5' apply false
    id 'com.github.ben-manes.versions' version '0.29.0'
}

def micronautModules = [project(':rest-bridge-micronaut'), project(':examples:micronaut-app')]
def springModules = [project(':rest-bridge-spring-boot-starter'), project(':examples:spring-boot-app')]
def publishModules = [project(':rest-bridge-core'), project(':rest-bridge-micronaut'), project(':rest-bridge-spring-boot-starter')]

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'

    group = 'io.github.artemy.osipov.thrift'

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    repositories {
        jcenter()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'

        testImplementation 'org.codehaus.groovy:groovy-test:3.0.5'

        def junitVersion = '5.6.2'
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    }

    test {
        useJUnitPlatform()
    }
}

configure(micronautModules) {
    dependencies {
        def micronautBom = 'io.micronaut:micronaut-bom:2.0.1'
        annotationProcessor platform(micronautBom)
        implementation platform(micronautBom)
        testAnnotationProcessor platform(micronautBom)
    }
}

configure(springModules) {
    dependencies {
        def springBootBom = 'org.springframework.boot:spring-boot-dependencies:2.3.2.RELEASE'
        implementation platform(springBootBom)
        annotationProcessor platform(springBootBom)
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
    }
}

configure(publishModules) {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = ['maven']
        pkg {
            repo = 'oss'
            name = project.name
            licenses = ['MIT']
            desc = 'Thrift-Rest bridge for proxy thrift services'
            websiteUrl = 'https://github.com/artemy-osipov/thrift-rest-bridge'
            issueTrackerUrl = 'https://github.com/artemy-osipov/thrift-rest-bridge/issues'
            vcsUrl = 'https://github.com/artemy-osipov/thrift-rest-bridge.git'

            githubRepo = 'artemy-osipov/thrift-rest-bridge'
            githubReleaseNotesFile = 'README.md'
        }
    }
}
